 
options {
   LOOKAHEAD=1;
   JAVA_UNICODE_ESCAPE=true;
}

PARSER_BEGIN(ModelGenerator)

package uti.parser;

import java.io.*;
import uti.codeModel.*;
import uti.java.*;
import java.util.*;

class TypeInfo 
{
   LinkName name;
   int arraycount=0;
}

public class ModelGenerator {
   static public UtiPackage MainPackage=null;
   static ModelGenerator mg=null;
   public static void readSingleFile(File f, UtiPackage m)
   {
       try {
       BufferedReader in =new BufferedReader(new FileReader(f.getAbsoluteFile()));
       System.out.println("Datei "+f.getAbsoluteFile()+" geladen");
       if (mg == null) {
          mg = new ModelGenerator(in);
       } else {
          mg.ReInit(in);
       }
       System.out.println("ModelGenerator gestartet");
       mg.MainPackage = m;
       mg.program();
       System.out.println("Programm geparsed");
       } catch(Exception e) {
       e.printStackTrace();
       }
   }
   static UtiPackage getPackage(LinkName n)
   {
      UtiPackage current=MainPackage;
      for (int i = 0; i < n.getCount(); i++) {
         String b = n.getValue(i);
         UtiPackage ne = current.getPackageByName(b);
         if (ne== null) ne = current.addPackage(b);
         current = ne;
      }
      return current;
   }

}

PARSER_END(ModelGenerator)

SKIP:
{
" "
| "\r"
| "\t"
| "\n"
}

MORE: 
{
   "/*": BIG_COMMENT
|   "//": SMALL_COMMENT
}

<BIG_COMMENT>
SPECIAL_TOKEN:
{
   <BIG_COMMENT_BLA: "*/" > : DEFAULT
}

<SMALL_COMMENT>
SPECIAL_TOKEN:
{
   <SMALL_COMMENT_BLA: "\n" | "\r" > : DEFAULT
}

<BIG_COMMENT, SMALL_COMMENT>
MORE:
{
   < ~[]>
}

//Komplexe Elemente


TOKEN: {
   <INTEGER_VALUE:
      ["1"-"9"] (["0"-"9"])* (["l", "L"])?
      | "0" ["x", "X"]  (["0"-"9", "a"-"f", "A"-"F"])+ (["l", "L"])?
      | "0" (["0"-"7"])*
   >
   |
   <FLOAT_VALUE:
      (["0"-"9"])+ "." (["0"-"9"])* (<EXP>)? (["f", "F", "d", "D"])?
      |  "." (["0"-"9"])+ (<EXP>)? (["f", "F", "d", "D"])?
      |  (["0"-"9"])+ <EXP> (["f", "F", "d", "D"])?
      |  (["0"-"9"])+ (<EXP>)? ["f", "F", "d", "D"]
   >
   |
   <#EXP:["e", "E"] (["+", "-"])? (["0"-"9"])+ >
   |
   <CHAR_VALUE:  
      "'"
      (   (~["'", "\\", "\n", "\r"])
        | ("\\"
            ( ["0"-"7"] ( ["0"-"7"] ) ?
            | ["n", "t", "b", "r", "f", "\\", "'", "\""]
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
         )
      )
      "'"
    >   
    |
    <STRING_VALUE:  
      "\""
      (   (~["'", "\\", "\n", "\r"])
        | ("\\"
            ( ["0"-"7"] ( ["0"-"7"] ) ?
            | ["n", "t", "b", "r", "f", "\\", "'", "\""]
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
         )
      )*
      "\""
    >   
    |
    <CODE_VALUE:  
      "ยง"
      ( ~["ยง"])*
      "ยง"
    >  
}


TOKEN:
{
   <PACKAGE_TOK: "package" >
   |<CLASS_TOK: "class" >
   |<INTERFACE_TOK: "interface" >
   |<PUBLIC_TOK: "public" >
   |<PROTECTED_TOK: "protected" >
   |<PRIVATE_TOK: "private" >
   |<FINAL_TOK: "final">
   |<ABSTRACT_TOK: "abstract">
   |<IMPLEMENTS_TOK: "implements">
   |<EXTENDS_TOK: "extends">
   |<THROWS_TOK: "throws">
   |<STATIC_TOK: "static">
   |<IF_TOK: "if">
   |<ELSE_TOK: "else">
   |<DO_TOK: "do">
   |<WHILE_TOK: "while">
   |<FOR_TOK: "for">
   |<INSTANCEOF_TOK: "instanceof">
   |<TRUE_TOK: "true">
   |<FALSE_TOK: "false">
   |<NULL_TOK: "null">
   |<EXTERN_TOK: "externcode">
   |<RETURN_TOK: "return">
   |<BREAK_TOK: "break">
   |<CONTINUE_TOK: "continue">
   |<NEW_TOK: "new">
   
}

TOKEN:
{
   <IDENT: ["$", "A"-"Z", "_", "a"-"z"] 
   (
      ["0"-"9",
      "$","A"-"Z", "_", "a"-"z"]
   )*
   >
}

TOKEN:
{
   <LKLAMMER: "(">
 | <RKLAMMER: ")">
 | <LGKLAMMER: "{" >
 | <RGKLAMMER: "}" >
 | <LEKLAMMER: "]" >
 | <REKLAMMER: "[" >
 | <SEMICOLON: ";" >
 | <COMMA: ",">
 | <DOT: ".">
}

void program():
{
   LinkName n=null;
   UtiPackage currentpackage=MainPackage;
   UtiClass c=null;
   UtiInterface i=null;
}
{
   ("package" n = name()  ";"
      {
         currentpackage = getPackage(n);      
      }

   )?
   (
      c=StdClass() {currentpackage.addChild(c);}
      | i = StdInterface() {currentpackage.addChild(i);}
      | ";"
   )+
}

LinkName name():
{
   LinkName n=new LinkName();
   Token bez;
}
{
   (bez=<IDENT> {n.addValue(bez.toString(), bez);}
   (
      "." bez=<IDENT> {n.addValue(bez.toString(), bez);}
   )*)
   {return n;}
}

LinkName singlename():
{
   LinkName n=new LinkName();
   Token bez;
}
{
   bez=<IDENT> {n.addValue(bez.toString(), bez);}
   
   {return n;}
}

UtiClass StdClass():
{
   UtiClass cl=null;
   boolean abst= false;
   boolean publ = false;
   boolean fin=false;
}
{
  ("public" {publ=true;})? 
   (
     "abstract" {abst = true;}
     | "final" {fin = true;}
   )?
   cl=Klassendeklaration() 
   {
   cl.setPublic(publ);
   cl.setAbstract(abst);
   cl.setFinal(fin);   
   return cl;
   } 
}

UtiInterface StdInterface():
{
}
{
   "interface" {return null;}
}

int Sichtbarkeitsmod():
{
   int vis = -1;
}
{
   (
         "public" {vis = 0;}
         | "protected" {vis = 1;}
         | "private" {vis = 2;}
      )?
     {return vis;}
}

UtiClass Klassendeklaration():
{
   UtiClass cl=new UtiClass(null);
   Token cn;
   LinkName ln;
   BaseName bn;
   Vector blist;
   int vis=-1;
}
{
   "class" cn=<IDENT> {cl.setName(cn.toString());}
   ("extends" ln=name()
      {
         LinkMemory.add(cl.intern_extends_link(), cl, ln, UtiClass.class);
      }
   )?
   ("implements" ln=name()
       {
         LinkMemory.add(cl.intern_impl_link(), cl, ln, UtiInterface.class);
       }
     ("," ln=name()
        {
           LinkMemory.add(cl.intern_impl_link(), cl, ln, UtiInterface.class);
        }     
     )*  
   )?
   "{"
   (
      vis =  Sichtbarkeitsmod()
      (
      LOOKAHEAD( MethodeLookahead() )
         bn=Methode() {
            cl.addChild(bn);
      }
         |  LOOKAHEAD( <IDENT> "(" )
            bn=Constructor() {
            cl.addChild(bn);
      }
         | blist=Deklaration() ";" {
            for (int i = 0; i < blist.size(); i++) {
               cl.addChild((BaseName)blist.elementAt(i));
            }
      }
         | "abstract" bn=Methodenrumpf(){
            cl.addChild(bn);
      }
      ) 
      | "static"
      | bn = ExternCode() {cl.addChild(bn);}
      
   )*
   "}"
   {return cl;}
   
   
}

void MethodeLookahead():
{}
{
  TypeDeklaration() <IDENT> "("
}

Vector Deklaration():
{
   LinkName typ;
   int arraycount=0;
   UtiVariable var;
   Vector l = new Vector();
}
{
   (typ=name() ("[""]" {arraycount++;} )* 
   var=Variablendeklaration() {      
      LinkMemory.add(var.intern_type(), arraycount, var, typ, UtiType.class);
      l.addElement(var);
   }
   ( ","
   var=Variablendeklaration() {
      LinkMemory.add(var.intern_type(), arraycount, var, typ, UtiType.class);
      l.addElement(var);
   }
   
   )*)
   {return l;}
}

UtiVariable SingleDeklaration():
{
   LinkName typ;
   int arraycount=0;
   UtiVariable var;
}
{
   typ=name() ("[""]" {arraycount++;} )* 
   var=Variablendeklaration() {
      LinkMemory.add(var.intern_type(), arraycount, var, typ, UtiType.class);
      
   }
   {return var;}
}


TypeInfo TypeDeklaration():
{
   TypeInfo d= new TypeInfo();
}
{
   d.name=name() ( "[""]" {d.arraycount++;} )* 
   {
       return d;
   }
}

UtiVariable Variablendeklaration():
{
   UtiVariable var=new UtiVariable(null);
   Token n;
}
{
   n=<IDENT> {var.setName(n.toString());}

  {return var;}
}

Vector FormaleParameter():
{
   UtiVariable var;
   Vector l = new Vector();
   TypeInfo t;
}
{
   "("
   (
   t = TypeDeklaration()
   var=Variablendeklaration() {
      LinkMemory.add(var.intern_type(), t.arraycount, var, t.name, UtiType.class);    
      l.addElement(var);
   })?
   ( "," 
   t = TypeDeklaration()
   var=Variablendeklaration() {
      LinkMemory.add(var.intern_type(), t.arraycount, var, t.name, UtiType.class);
      l.addElement(var);

   }
   
   )*
   ")"
   {return l;}
}

UtiMethod MethodKopf():
{
   UtiMethod m = new UtiMethod(null);
   Vector v = new Vector();
   Token n;
   LinkName link;
}
{
   n = <IDENT> {m.setName(n.toString());}
   v=FormaleParameter() 
   { 
      for (int i = 0; i < v.size(); i++) {
         m.addChild((UtiVariable)v.elementAt(i));
      };
   }
   (
      "throws"
      link = name() 
      {
         LinkMemory.add(m.intern_throw(), m, link, UtiClass.class);
      }
      (
      
      ","
      link = name() 
      {
         LinkMemory.add(m.intern_throw(), m, link, UtiClass.class);
      }      
      )*
   
   )?
   {return m;}
}

UtiMethod Constructor():
{
   UtiMethod m = null;
   UtiBlock b=null;
}
{
   m=MethodKopf()
   b=Block()
   {m.setConstructor(true); m.setBlock(b); return m;}
}

UtiMethod Methode():
{
   UtiMethod m = null;
   UtiBlock b=null;
}
{
   m=Methodenrumpf()
   b=Block()
   {m.setBlock(b); return m;}
}

UtiMethod Methodenrumpf():
{
   UtiMethod m = null;
   TypeInfo t;
}
{
   t=TypeDeklaration()
   m = MethodKopf()
   {
   //   LinkMemory.add(m.intern_result(), m, typ, UtiType.class);
   LinkMemory.add(m.intern_result(), t.arraycount, m, t.name, UtiType.class);
   return m;
   }
}


UtiBlock Block():
{
   UtiBlock b=new UtiBlock(null);
   BaseCommand com;
   Vector v;
}
{
   
   ("{"
   (
      LOOKAHEAD(Deklaration() ";") v=Deklaration() ";"
      {
         for (int i = 0; i < v.size(); i++)  {
            b.addCommand((UtiVariable)v.elementAt(i));
         }
      }
      |     
      com=Basisanweisung()
      {
         b.addCommand(com);
      }
   )*
   "}"
   |
   (
      LOOKAHEAD(Deklaration() ";") v=Deklaration() ";"
      {
         for (int i = 0; i < v.size(); i++)  {
            b.addCommand((UtiVariable)v.elementAt(i));
         }
      }
      |     
      com=Basisanweisung()
      {
         b.addCommand(com);
      }
   )
   )
   {return b;}
}

BaseCommand Basisanweisung():
{
   BaseCommand com=null;
}
{
   (com=If()
   | com=For()
   | com=While()
   | com=DoWhile()
   | com=ExternCode()
   | com=Return()
   | com=Break()
   | com=Continue()
   | com=Ausdruck() ";" 
   | ";")
   { 
   return com;
   }
}

UtiExtern ExternCode():
{
   UtiExtern result = null;
   Token t1, t2;
}
{
   "externcode" "(" t1=<STRING_VALUE>")" t2 =<CODE_VALUE>
   {
   result = new UtiExtern();
   result.setType(t1.toString().substring(1, t1.toString().length()-1));
   result.setCode(t2.toString().substring(1, t2.toString().length()-1));
   return result;}

}

/*BaseCommand Zuweisung():
{
   UtiZuweisung z;
   UtiAusdruck a;
   UtiDesignator d;
}
{
   d = Designator() 
   (" = "
   a = Ausdruck()
   {
   z = new UtiZuweisung(null);
   z.setExpression(a);
   z.setTarget(d);
   return z;
   }
   )?
   {return d;}
}*/

UtiSpecialCommand Break():
{
   UtiSpecialCommand f = new UtiSpecialCommand(null);
   UtiAusdruck d;
}
{
   "break" ";"
  {f.setType(UtiSpecialCommand.BREAK);
  return f;}
}

UtiSpecialCommand Continue():
{
   UtiSpecialCommand f = new UtiSpecialCommand(null);
   UtiAusdruck d;
}
{
   "continue" ";"
  {f.setType(UtiSpecialCommand.CONTINUE);
  return f;}
}

UtiSpecialCommand Return():
{
   UtiSpecialCommand f = new UtiSpecialCommand(null);
   UtiAusdruck d;
}
{
   "return" [d=Ausdruck(){f.setAusdruck(d);}] ";"
  {f.setType(UtiSpecialCommand.RETURN);
  return f;}
}

UtiIf If():
{
   UtiIf f = new UtiIf(null);
   UtiAusdruck d;
   UtiBlock b;
}
{
   "if" "("d=Ausdruck() ")" {f.setExpression(d);}
   b=Block() {f.setThen_Block(b);}
   (
   LOOKAHEAD("else")
      "else"
      b=Block() {f.setElse_Block(b);}
   )?

  {return f;}
}

UtiFor For():
{
   UtiFor f = new UtiFor(null);
   UtiAusdruck d;
   UtiBlock b;
   UtiVariable v;
}
{
   "for" "(" 
   [ 
    (
    LOOKAHEAD(2)
    d=Ausdruck() {f.addInit(d);}
    |v=SingleDeklaration() {f.addInit(v);}
    )]
    (","(LOOKAHEAD(2)
    d=Ausdruck() {f.addInit(d);}
        |v=SingleDeklaration() {f.addInit(v);}
       ))*    
       
   ";" d=Ausdruck() {f.setBedingung(d);}
   ";" 
   [d=Ausdruck() {f.addUpdate(d);}]
    (","d=Ausdruck() {f.addUpdate(d);})* ")"
   b=Block() {f.setBlock(b);}
   {return f;}
}

UtiWhile While():
{
   UtiWhile w = new UtiWhile(null);
   UtiAusdruck d;
   UtiBlock b;
}
{
   "while" "(" d = Ausdruck() ")" b= Block() 
   {
      w.setExpression(d);
      w.setBlock(b);
      return w;
   }
}

UtiWhile DoWhile():
{
   UtiWhile w = new UtiWhile(null);
   UtiAusdruck d;
   UtiBlock b;
}
{
   "do" b= Block() "while" "(" d = Ausdruck() ")"
   {
      w.setExpression(d);
      w.setBlock(b);
      w.setDoWhile(true);
      return w;
   }
}

/*UtiDesignator Designator():
{
   UtiDesignator d = new UtiDesignator(null);
   Vector elements=new Vector();
   LinkName n;
   UtiAusdruck a;
   Vector arguments;
}
{
  ( n=name() {elements.addElement(n);}
      ( 
      ("[" a= Ausdruck() "]" {elements.addElement(a);})+
      | "(" arguments=Arguments() ")" {elements.addElement(arguments);}
      )?
   (
      "."
      n=name() {elements.addElement(n);}
      ( 
      ("[" a= Ausdruck() "]" {elements.addElement(a);})+
      | "(" arguments=Arguments() ")" {elements.addElement(arguments);}
      )?
   
   )*
   )

   {
   LinkMemory.addDesignator(d, elements);
   return d;
   }
}

UtiDesignator ContinueDesignator():
{
   UtiDesignator d = new UtiDesignator(null);
   Vector elements=new Vector();
   LinkName n;
   UtiAusdruck a;
   Vector arguments;
}
{
  ( "." n=name() {elements.addElement(n);}
      ( 
       "(" arguments=Arguments() ")" {elements.addElement(arguments);}
      )?
      |( "[" a= Ausdruck() "]" {elements.addElement(a);})+
       
   (
      "."
      n=name() {elements.addElement(n);}
      ( 
      ("[" a= Ausdruck() "]" {elements.addElement(a);})+
      | "(" arguments=Arguments() ")" {elements.addElement(arguments);}
      )?
   
   )*
   )

   {
   LinkMemory.addContinueDesignator(d, elements);
   return d;
   }
}
*/

Vector Arguments():
{
   Vector a= new Vector();
   UtiAusdruck aus;
}
{
   aus = Ausdruck() {a.addElement(aus);}
   (
   "," aus = Ausdruck() {a.addElement(aus);}
   )*
   {return a;}
}

UtiAusdruck Ausdruck():
{
   UtiAusdruck a;
}
{
   a=Ausdruck1()
   {return a;}
}

UtiAusdruck Ausdruck1():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck2() [("="{value=UtiAusdruck.BI_SET;}|
                   "+="{value=UtiAusdruck.BI_SETADD;}|
                   "-="{value=UtiAusdruck.BI_SETSUB;}|
                   "*="{value=UtiAusdruck.BI_SETMUL;}|
                   "/="{value=UtiAusdruck.BI_SETDIV;}|
                   "%="{value=UtiAusdruck.BI_SETMODULO;}|
                   ">>="{value=UtiAusdruck.BI_SETRSHIFT;}|
                   ">>>="{value=UtiAusdruck.BI_SETRROUNDSHIFT;}|
                   "<<="{value=UtiAusdruck.BI_SETLSHIFT;}|
                   "&="{value=UtiAusdruck.BI_SETAND;}|
                   "|="{value=UtiAusdruck.BI_SETOR;}|
                   "^="{value=UtiAusdruck.BI_SETXOR;})
       c= Ausdruck1()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}

UtiAusdruck Ausdruck2():
{
   UtiAusdruck a, b, c;
}
{
   a=Ausdruck3() ["?" b= Ausdruck3() ":" c=Ausdruck3()
   {
      a= new UtiAusdruck(a, b, c, UtiAusdruck.BI_QUESTION);
   }]
   
   {return a;}
}

UtiAusdruck Ausdruck3():
{
   UtiAusdruck a, c;
}
{
   a=Ausdruck4() ["||" c= Ausdruck3()
   {
      a= new UtiAusdruck(a, c, UtiAusdruck.BI_LOGICOR);
   }]
   
   {return a;}
}

UtiAusdruck Ausdruck4():
{
   UtiAusdruck a, c;
}
{
   a=Ausdruck5()[ "&&" c= Ausdruck4()
   {
      a= new UtiAusdruck(a, c, UtiAusdruck.BI_LOGICAND);
   }]
   {return a;}
}

UtiAusdruck Ausdruck5():
{
   UtiAusdruck a, c;
}
{
   a=Ausdruck6()[ "|" c= Ausdruck5()
   {
      a= new UtiAusdruck(a, c, UtiAusdruck.BI_OR);
   }]
   
   {return a;}
}

UtiAusdruck Ausdruck6():
{
   UtiAusdruck a, c;
}
{
   a=Ausdruck7() ["^" c= Ausdruck6()
   {
      a= new UtiAusdruck(a, c, UtiAusdruck.BI_XOR);
   }]
   
   {return a;}
}

UtiAusdruck Ausdruck7():
{
   UtiAusdruck a, b,c;
}
{
   a=Ausdruck8() ["&" c= Ausdruck7()
   {
      a= new UtiAusdruck(a, c, UtiAusdruck.BI_AND);
   }]
 
   {return a;}
}

UtiAusdruck Ausdruck8():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck9() [("=="{value=UtiAusdruck.BI_EQUAL;}|
                   "!="{value=UtiAusdruck.BI_NOTEQUAL;})
       c= Ausdruck8()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}

UtiAusdruck Ausdruck9():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck10() [("<"  {value=UtiAusdruck.BI_LESS;}      |
                   "<="  {value=UtiAusdruck.BI_ELESS;}      |
                   ">"  {value=UtiAusdruck.BI_GREATER;}      |
                   ">="  {value=UtiAusdruck.BI_EGREATER;}      |
                   "instanceof"   {value=UtiAusdruck.BI_INSTANCEOF;}  )
       c= Ausdruck9()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}

UtiAusdruck Ausdruck10():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck11() [("<<"  {value=UtiAusdruck.BI_LSHIFT;}      |
                   ">>"  {value=UtiAusdruck.BI_RSHIFT;}      |
                   ">>>"  {value=UtiAusdruck.BI_SHIFTROUND;}    
                     )
       c= Ausdruck10()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}


UtiAusdruck Ausdruck11():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck12() [("+"  {value=UtiAusdruck.BI_PLUS;}      |
                   "-"  {value=UtiAusdruck.BI_MINUS;}        
                     )
       c= Ausdruck11()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}


UtiAusdruck Ausdruck12():
{
   UtiAusdruck a, c;
   int value;
}
{
   a=Ausdruck13() [("*"  {value=UtiAusdruck.BI_MULTIPLY;}      |
                   "/"  {value=UtiAusdruck.BI_DIVIDE;}      |
                   "%"  {value=UtiAusdruck.BI_MOD;}    
                     )
       c= Ausdruck12()
       {
          a= new UtiAusdruck(a, c, value);
       }]
   {return a;}
}

UtiAusdruck Ausdruck13():
{
   UtiAusdruck a, b,c;
   TypeInfo t;
   int value;
   Link l;
}
{
   (("~"  {value=UtiAusdruck.BI_BITNOT;}      |
    "!"  {value=UtiAusdruck.BI_NOT;}      |
    "+"  {value=UtiAusdruck.BI_NONE;}   |
    "-"  {value=UtiAusdruck.BI_NEG;}   |  
    "++"  {value=UtiAusdruck.BI_LEFTINCREMENT;}   |
    "--"  {value=UtiAusdruck.BI_LEFTDECREMENT;}  
    )b= Ausdruck13()
   {
      if (value != UtiAusdruck.BI_NONE) {
      a= new UtiAusdruck(b, value);
      } else {
      a=b;
      }
   }
   |LOOKAHEAD(TypeDeklaration() Ausdruck13()) "(" t=TypeDeklaration() ")" c= Ausdruck13()
   {
      l = new Link();
      a= new UtiAusdruck(l, c);
      LinkMemory.add(l, t.arraycount, a, t.name, UtiType.class);
      
   }
   | a=Ausdruck14() [("++"  {value=UtiAusdruck.BI_RIGHTINCREMENT;}   |
                     "--"  {value=UtiAusdruck.BI_RIGHTDECREMENT;}  
                    )
   {
      a= new UtiAusdruck(a,value);
   }])
   {return a;}
}

UtiAusdruck Ausdruck14():
{
   UtiAusdruck a=null, b;
   Token t;
   LinkName link;
   Link n;
   Vector v = null;
}
{
   
   (LOOKAHEAD("new" singlename() "[") "new" link=singlename()
       {v = new Vector();}
       ("[" b=Ausdruck() "]" {v.addElement(b);} )+
      {
        n=new Link();
        a=new UtiAusdruck(UtiAusdruck.BI_CONSTRUCTORARRAY);
        a.addData(n);
        LinkMemory.add(n, a, link, UtiType.class);
        for (int i = 0; i < v.size(); i++) {
              a.addData((UtiOB)v.elementAt(i));
        }
     }
   |
   
   a=Ausdruck15()
   
   (
   "." (link=singlename() ["("
        {v = new Vector();}
        [b=Ausdruck() {v.addElement(b);}]
        ("," b=Ausdruck() {v.addElement(b);}
        )*
       ")"])
     { 
        n=new Link();
        b=a;
        if (v == null) {
           a=new UtiAusdruck(UtiAusdruck.BI_FIELD);
           a.addData(b);
           a.addData(n);
           LinkMemory.addBase(b,n, a, link, UtiVariable.class);
        } else {
           a=new UtiAusdruck(UtiAusdruck.BI_METHOD);
           a.addData(b);
           a.addData(n);
           LinkMemory.addBase(b,n, a, link, UtiMethod.class);
           for (int i = 0; i < v.size(); i++) {
              a.addData((UtiOB)v.elementAt(i));
           }
        }
     }
   | "["b=Ausdruck() "]" {a = new UtiAusdruck(a, b, UtiAusdruck.BI_ARRAY);}
   
   )*)
   {return a;}
}

UtiAusdruck Ausdruck15():
{
   UtiAusdruck a=null, b;
   Token t;
   LinkName link;
   Link n;
   Vector v = null;
   String s;
}
{
   ("(" a=Ausdruck() ")" 
   | "true" {a = new UtiAusdruck(UtiAusdruck.BI_TRUE);}
   | "false" {a = new UtiAusdruck(UtiAusdruck.BI_FALSE);}
   | "null" {a = new UtiAusdruck(UtiAusdruck.BI_NULL);}
   | t=<INTEGER_VALUE> {
   a = new UtiAusdruck(new Integer(Integer.parseInt(t.toString())));
   }
   | t=<FLOAT_VALUE> {a = new UtiAusdruck(new Double(Double.parseDouble(t.toString())));}
   | t=<STRING_VALUE> {s = t.toString(); s = s.substring(1, s.length()-1); a= new UtiAusdruck(s);}
   | t=<CHAR_VALUE> {a= new UtiAusdruck(new Character(t.toString().charAt(1)));}
   |  (link=singlename() ["("
        {v = new Vector();}
        [b=Ausdruck() {v.addElement(b);}]
        ("," b=Ausdruck() {v.addElement(b);}
        )*
       ")"])
     { 
        n=new Link();
        if (v == null) {
           a=new UtiAusdruck(UtiAusdruck.BI_VARIABLE);
           a.addData(n);
           LinkMemory.add(n, a, link, UtiVariable.class);
        } else {
           a=new UtiAusdruck(UtiAusdruck.BI_CALL);
           a.addData(n);
           LinkMemory.add(n, a, link, UtiMethod.class);
           for (int i = 0; i < v.size(); i++) {
              a.addData((UtiOB)v.elementAt(i));
           }
        }
     }
   | LOOKAHEAD("new" singlename() "(") 
               "new" link=singlename() "("
        {v = new Vector();}
        [b=Ausdruck() {v.addElement(b);}]
        ("," b=Ausdruck() {v.addElement(b);}
        )*
       ")"
     {
        n=new Link();
        a=new UtiAusdruck(UtiAusdruck.BI_CONSTRUCTOR);
        a.addData(n);
        LinkMemory.add(n, a, link, UtiType.class);
        for (int i = 0; i < v.size(); i++) {
              a.addData((UtiOB)v.elementAt(i));
        }
     })
   {return a;}
}